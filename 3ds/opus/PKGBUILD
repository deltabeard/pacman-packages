# Maintainer:  Mahyar Koshkouei <mk@deltabeard.com>

pkgname=3ds-opus
pkgver=1.2.1
pkgrel=1
pkgdesc='Totally open, royalty-free, highly versatile audio codec'
arch=('any')
url='https://opus-codec.org/'
license=(BSD)
options=(!strip libtool staticlibs)
source=("https://archive.mozilla.org/pub/opus/opus-$pkgver.tar.gz")
sha256sums=('cfafd339ccd9c5ef8d6ab15d7e1a412c054bf4cb4ecbbbcc78c12ef2def70732')
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')

build() {
  cd opus-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  ./configure \
    --prefix="${PORTLIBS_PREFIX}" \
    --host="arm-none-eabi" \
    --disable-shared --enable-static 

  make
}

package() {
  cd opus-$pkgver

  make DESTDIR="$pkgdir" install
}
